
plugins {
	id 'jacoco'
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.openapi.generator' version '7.1.0' apply false
	id 'com.diffplug.spotless' version "7.0.0.BETA4" apply false
}

repositories {
	mavenCentral()
}

allprojects {
	group = 'ua.userfusion'

	ext {
		versionSpringCloud = '2023.0.2'
		versionOpenApiWebMvcUI = '2.5.0'
		versionSpringBootDepend = '3.2.5'
	}
}
subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'checkstyle'

	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	jar {
		archiveBaseName = "${project.name}"
	}

	tasks.jar {
		enabled = true
		// Remove `plain` postfix from jar file name
		archiveClassifier.set("")
	}


	apply plugin: 'jacoco'

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${versionSpringCloud}"
		}
	}

	test {
        useJUnitPlatform {
			includeEngines("junit-jupiter")
		}
		finalizedBy jacocoTestReport
	}

	jacocoTestReport {
		dependsOn test
		reports {
			xml.setRequired(true)
		}
	}

	tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
		options.fork = true
	}

	tasks.withType(Copy).configureEach {
		duplicatesStrategy = 'EXCLUDE'
	}
}

bootJar {
	enabled = false
}
